{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "# Skip-Entry Dynamics\n",
        "\n",
        "Joe(y) Carpinelli  \n",
        "12/10/22\n",
        "\n",
        "> **Note**\n",
        ">\n",
        "> The content here was originally written as part of a problem set\n",
        "> assigned in a graduate launch and entry vehicle design course at the\n",
        "> University of Maryland. The problem statement was educationally false,\n",
        "> and I believe this was deliberate! Either way, I’m thankful for that\n",
        "> slight falsehood; it cemented my early understanding of spacecraft\n",
        "> entry dynamics, and has motivated this whole post.\n",
        "\n",
        "## Artemis 1\n",
        "\n",
        "Go Artemis! NASA’s first Artemis mission launched successfully in the\n",
        "earliest hours of November 16th, 2022. Artemis 1 was an uncrewed flight\n",
        "*test* — the [Orion\n",
        "spacecraft](https://www.nasa.gov/exploration/systems/orion/index.html)\n",
        "flew to the moon, arrived in a special kind of stable orbit called a\n",
        "[*Distant Retrograde\n",
        "Orbit*](https://www.nasa.gov/feature/orion-will-go-the-distance-in-retrograde-orbit-during-artemis-i).\n",
        "Of course, NASA’s flight operators in Mission Control captured [stunning\n",
        "pictures](https://www.nasa.gov/content/artemis-1-images) along the way.\n",
        "A visual overview for Artemis 1 is shown in [Figure 1](#fig-a1-arch).\n",
        "\n",
        "Artemis 1 is [record\n",
        "breaking](https://www.theverge.com/2022/11/29/23484571/artemis-1-halfway-record-distance)\n",
        "through its furthest achieved distance from Earth, and through one of\n",
        "its most important test objectives — the first [successfully\n",
        "test](https://www.lockheedmartin.com/en-us/news/features/2022/orion-heat-shield.html)\n",
        "of a skip-entry for a human-rated spacecraft. Orion’s heatshield will be\n",
        "tested as it enters Earth atmosphere on December 11th, 2022.\n",
        "\n",
        "![](https://www.nasa.gov/sites/default/files/thumbnails/image/mission_profile_simple_artemis_i_droarticle_acrane.jpg)\n",
        "\n",
        "Figure 1: Artemis 1 Mission Architecture\n",
        "\n",
        "## Atmospheric Entry\n",
        "\n",
        "Testing a new spacecraft’s capability to safely enter Earth’s atmosphere\n",
        "is incredibly important. If a spacecraft is to return to Earth in-tact,\n",
        "it has to survive the fall through Earth’s atmosphere. Returning\n",
        "spacecraft are hurtling through space at tens of thousands of miles per\n",
        "hour; as the spacecraft collide with air particles at high speeds, an\n",
        "enormous amount of heat is generated. To withstand the heat of entering\n",
        "a planet’s atmosphere from space, spacecraft commonly have a thick metal\n",
        "component which is oriented towards the atmosphere for the duration of\n",
        "the atmospheric entry. This metal hardware is referred to as a heat\n",
        "shield. Artemis 2 will fly four astronauts to a lunar orbit before\n",
        "returning back to Earth. A successful atmospheric entry in Artemis 1\n",
        "will prove the Orion spacecraft capable of safely returning astronauts\n",
        "back to Earth in Artemis 2, and future Artemis missions.\n",
        "\n",
        "The *angle* of a spacecraft’s flight into Earth’s atmosphere (mostly)\n",
        "determines the *kind* of atmospheric entry: ballistic, lifting, or\n",
        "skipping. A steep entry will cause the spacecraft to *cannonball*\n",
        "through Earth’s atmosphere; if we model this scenario while *ignoring\n",
        "the force of gravity on the spacecraft*, the spacecraft’s trajectory is\n",
        "*ballistic*. A slightly less steep entry will allow the atmosphere to\n",
        "*lift* the spacecraft as it descends; this force exerted by the\n",
        "atmosphere on the spacecraft is similar to how airplanes fly, and this\n",
        "flavor of atmospheric entry is known as *lifting* entry. Finally,\n",
        "entering the atmosphere at a shallow angle will cause the spacecraft to\n",
        "*skip* across the atmosphere like a rock on a pond; this is known as a\n",
        "*skipping* entry.\n",
        "\n",
        "No human-rated vehicle as *ever* completed a skip-entry before\n",
        "Artemis 1. Orion will be the first! There are [many\n",
        "benefits](https://www.lockheedmartin.com/en-us/news/features/2022/orion-skip-maneuver.html)\n",
        "to skip-entries, including lower accelerations experienced by\n",
        "astronauts, more precise landing targets, and lower temperatures on the\n",
        "heat shield. While the benefits of skip-entries are real, all\n",
        "atmospheric entries are incredibly sensitive. Enter the atmosphere at\n",
        "too steep an angle, and the spacecraft will burn up in the atmosphere.\n",
        "Enter the atmosphere at too shallow an angle, and the spacecraft will\n",
        "skip too far and fly out into space, never to return. Rigorous\n",
        "calculations must be made to ensure the spacecraft is entering the\n",
        "atmosphere at the right angle, and the right speed. With a few\n",
        "simplifying assumptions, the math behind atmospheric entry can be simple\n",
        "enough to fit in a blog post! Don’t believe me? Read on!\n",
        "\n",
        "## Atmospheric Entry Dynamics\n",
        "\n",
        "We can simulate the Orion spacecraft’s trajectory through the atmosphere\n",
        "with just a couple dozen lines of code! To build a semi-accurate model\n",
        "for how a spacecraft enters Earth’s atmosphere, we’ll need to simplify\n",
        "the problem by make some assumptions. For example, let’s completely\n",
        "ignore winds! We should also ignore parachutes — we will just assume\n",
        "parachutes deploy successfully at *some* altitude. Let’s pretend the\n",
        "Orion spacecraft is completely rigid by ignoring the possibility of any\n",
        "bending or flexing in the structure that could affect the aerodynamics\n",
        "throughout entry. Also, we are leaving out all considerations related to\n",
        "heat! No thermodynamic modeling in this post. We will assume the heat\n",
        "shield is capable of handling any entry we throw at it.\n",
        "\n",
        "The word *model* might seem a bit abstract. What does a model look like?\n",
        "A model can be thought of as the combination of two things: simplifying\n",
        "assumptions, and equations which rely on the simplifying assumptions. So\n",
        "all we need to build a model is to list the assumptions we make, and use\n",
        "those assumptions to write equations. Hey! That’s only two things!\n",
        "\n",
        "There are many other assumptions we’re making implicitly in this\n",
        "problem. While we can’t possibly list them all, let’s go into some more\n",
        "depth for a couple of the most important assumptions our model will rely\n",
        "on.\n",
        "\n",
        "### Exponential Atmosphere\n",
        "\n",
        "Earth’s atmosphere will exert aerodynamic forces on the Orion\n",
        "spacecraft. If we ignore wind, these aerodynamic forces will depend on\n",
        "some aerodynamic coefficients, the spacecraft’s airspeed and\n",
        "orientation, as well as the *density* of the atmosphere. This\n",
        "atmospheric density will vary with altitude! We can create a *very*\n",
        "simple model for Earth’s atmosphere by assuming the density decays\n",
        "*exponentially* as altitude increases. The core equation for $\\rho$, the\n",
        "atmospheric density, will depend on altitude above sea level $h$, the\n",
        "atmospheric density at sea level $\\rho_0$, and a scaling factor $h_s$\n",
        "which is unique to each planet. For Earth, an accurate scaling factoris\n",
        "approximately equal to $7524$ meters.\n",
        "\n",
        "<span id=\"eq-exp-atm\">$$\n",
        "\\rho = \\rho_0 e^\\frac{-h}{h_s}\n",
        " \\qquad(1)$$</span>\n",
        "\n",
        "### Aerodynamic Coefficients\n",
        "\n",
        "The aerodynamic forces on Orion can be summarized by a few aerodynamic\n",
        "coefficients: the ballistic coefficient $\\beta$, the coefficient of drag\n",
        "$C_D$, and the lifting coefficient $C_L$. The coefficients of lift and\n",
        "drag are commonly combined into one value: the *lift to drag ratio*\n",
        "$C_R$. For this post, one value for Orion’s $C_R$ will be assumed. The\n",
        "ballistic coefficient can be calculated using the vehicle’s mass, lift\n",
        "to drag ratio, and the surface area of the heat shield, as shown in\n",
        "[Equation 3](#eq-ballistic-coeff).\n",
        "\n",
        "<span id=\"eq-l-to-d\">$$\n",
        "C_R = \\frac{C_L}{C_D}\n",
        " \\qquad(2)$$</span>\n",
        "\n",
        "<span id=\"eq-ballistic-coeff\">$$\n",
        "\\beta = \\frac{m}{C_R A_s}\n",
        " \\qquad(3)$$</span>\n",
        "\n",
        "Code which calculates the ballistic coefficient, and other calculations\n",
        "relevant to atmospheric entry dynamics, is provided below.\n",
        "\n",
        "``` julia\n",
        "\"\"\"\n",
        "Calculate the ballistic coefficient, β.\n",
        "\"\"\"\n",
        "β(m, Cᵣ, Aₛ) = m / (Cᵣ * Aₛ)\n",
        "\n",
        "\"\"\"\n",
        "Calculate the derivative of the downrange distance, ḋ.\n",
        "\"\"\"\n",
        "ḋ(ν, γ) = ν * cos(γ)\n",
        "\n",
        "\"\"\"\n",
        "Calculate the terminal velocity, vₜ.\n",
        "\"\"\"\n",
        "vₜ(g, β, ρ, γ) = √(-2g * β * sin(γ) / ρ)\n",
        "\n",
        "\"\"\"\n",
        "Calculate the maximum deceleration, nₘ.\n",
        "\"\"\"\n",
        "nₘ(vₑ, γ, hₛ, e) = (vₑ^2 / hₛ) * (sin(γ) / 2e)\n",
        "```\n",
        "\n",
        "### Equations of Motion\n",
        "\n",
        "With all of the assumptions described above, we can write the *equations\n",
        "of motion* for a spacecraft as it flies through a planet’s atmosphere.\n",
        "The equations shown through math and code below describe how **four**\n",
        "specific values change with time as the spacecraft flies: the angle of\n",
        "the spacecraft’s velocity with respect to the spacecraft’s horizontal\n",
        "axis $\\gamma$, the airspeed $v$, the distance to Earth’s center $r$, and\n",
        "the angle of the spacecraft’s position with respect to the horizontal\n",
        "along the Earth’s center $\\theta$.\n",
        "\n",
        "<span id=\"eq-core-eom\">$$\n",
        "\\begin{align} \n",
        "  \\dot{\\gamma} &= \\frac{1}{v} \\left( L_m - (1 - \\frac{v^2}{v_c^2}) g \\cos{\\gamma} \\right) \\\\ \n",
        "  \\dot{v} &= -D_m - g \\sin{\\gamma} \\\\ \n",
        "  \\dot{r} &= v \\sin{\\gamma} \\\\ \n",
        "  \\dot{\\theta} &= \\frac{v}{r} \\cos{\\gamma} \\\\ \n",
        "\\end{align}\n",
        " \\qquad(4)$$</span>\n",
        "\n",
        "The code below defines a function, `CanonicalEntry`, which produces the\n",
        "equations of motion for a spacecraft’s atmospheric entry along a plane\n",
        "*in code*."
      ],
      "id": "aebe73e9-b495-41c9-9cd6-de97d81c151c"
    },
    {
      "cell_type": "code",
      "execution_count": 3,
      "metadata": {},
      "outputs": [
        {
          "output_type": "display_data",
          "metadata": {},
          "data": {
            "text/plain": [
              "CanonicalEntry"
            ]
          }
        }
      ],
      "source": [
        "using Memoize: @memoize\n",
        "using Symbolics, ModelingToolkit\n",
        "using PhysicalConstants.CODATA2018: NewtonianConstantOfGravitation as G₀\n",
        "\n",
        "\"\"\"\n",
        "Construct a model for entry dynamics.\n",
        "\"\"\"\n",
        "@memoize function CanonicalEntry(; name = :CanonicalEntry, simplify = true, structural_simplify = true)\n",
        "\n",
        "  @variables t\n",
        "  \n",
        "  x = @variables γ(t) v(t) r(t) θ(t)\n",
        "  p = @parameters r₀ ρ₀ hₛ β Cᵣ μ g₀\n",
        "  δ = Differential(t)\n",
        "\n",
        "  vc = √(μ / r)\n",
        "  g = g₀ * (r₀ / r)^2\n",
        "  h = r - r₀\n",
        "  ρ = ρ₀ * exp(-h / hₛ)\n",
        "  Dₘ = (ρ / 2) * v^2 / β\n",
        "  Lₘ = Cᵣ / Dₘ\n",
        "\n",
        "  eqs = [\n",
        "    δ(γ) ~ (1/v) * (Lₘ - (1 - (v/vc)^2) * g * cos(γ)),\n",
        "    δ(v) ~ -Dₘ - g * sin(γ),\n",
        "    δ(r) ~ v * sin(γ),\n",
        "    δ(θ) ~ (v / r) * cos(γ)\n",
        "  ]\n",
        "\n",
        "  if simplify\n",
        "    map!(ModelingToolkit.simplify, eqs, eqs)\n",
        "  end\n",
        "\n",
        "  model = ODESystem(\n",
        "    eqs, t; name = name,\n",
        "  )\n",
        "\n",
        "  if structural_simplify\n",
        "    model = ModelingToolkit.structural_simplify(model)\n",
        "  end\n",
        "\n",
        "  return model\n",
        " \n",
        "end"
      ],
      "id": "172da7f0-5241-4778-924a-a9ee722089a2"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "Calling `CanonicalEntry` produces a model object, which we can inspect\n",
        "for the equations of motion written mathematically. This expands all of\n",
        "the equations; the output looks a lot more complicated than\n",
        "[Equation 4](#eq-core-eom)! Thank goodness for computers."
      ],
      "id": "55da9894-bc6f-47a5-9620-2e5c3c3f018e"
    },
    {
      "cell_type": "code",
      "execution_count": 4,
      "metadata": {},
      "outputs": [
        {
          "output_type": "display_data",
          "metadata": {},
          "data": {}
        }
      ],
      "source": [
        "model = CanonicalEntry()\n",
        "model |> equations .|> ModelingToolkit.simplify"
      ],
      "id": "4dc500aa-cb1c-4dc7-9234-e216f59e020d"
    }
  ],
  "nbformat": 4,
  "nbformat_minor": 5,
  "metadata": {
    "kernelspec": {
      "name": "julia-1.8",
      "display_name": "Julia 1.8.3",
      "language": "julia"
    },
    "language_info": {
      "name": "julia",
      "file_extension": ".jl",
      "mimetype": "application/julia",
      "version": "1.8.3"
    }
  }
}