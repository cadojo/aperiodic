{
  "cells": [
    {
      "cell_type": "raw",
      "metadata": {},
      "source": [
        "---\n",
        "title: \"Introduction to Dynamics\"\n",
        "description: \"_An introduction to dynamics through scientific software._\"\n",
        "date: \"2021-07-04\"\n",
        "file-modified: \"2022-08-14\"\n",
        "author: \"Joe(y) Carpinelli\"\n",
        "categories:\n",
        "  - dynamics\n",
        "  - controls\n",
        "  - offprint\n",
        "  - technical\n",
        "---"
      ],
      "id": "f3da7b1d"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        ":::{.callout-note appearance=\"simple\"}\n",
        "\n",
        "## Offprint\n",
        "\n",
        "This is a section from a control theory note-set I'm writing, **Exploring Control Theory**. \n",
        "It won't be done for years! Over time, I'll add chapters to the open-source repository, \n",
        "<https://github.com/cadojo/controls>. Some chapters that stand on their own will be \n",
        "added as blog posts [here](/blog/)!\n",
        "\n",
        ":::\n",
        "\n",
        "## Overview \n",
        "\n",
        "Our end goal as controls engineers is to _affect_ a system in some desired way. \n",
        "To do this, we'll need a mathematical description of our system. This mathematical \n",
        "description is known as a _model_. \n",
        "There's a famous [quote](https://en.wikipedia.org/wiki/All_models_are_wrong)\n",
        "about mathematical models: \"all models are wrong, but some are useful\". \n",
        "\n",
        "This is really important. We are __always__ approximating our system by describing it with a model.\n",
        "The question we need to ask is \"is our approximate description of our system (our model) good enough?\"\n",
        "\n",
        ":::{.callout-tip}\n",
        "## Definitions\n",
        "- __Model__: a mathematical description of a system\n",
        ":::\n",
        "\n",
        "But what does this model _look like_? To answer this question, let's \n",
        "first discuss system dynamics, and equations of motion.\n",
        "\n",
        "## Dynamics\n",
        "If we want to _affect_ a system, then by definition, the system _should_ be affect-able (is that a word?)\n",
        "by external forces. These \"forces\" don't necessarily need to be physical forces: consider the affect \n",
        "of introducing new predators in an ecosystem with the goal of affecting predator-prey populations.\n",
        "\n",
        "So we have a mathematical description, and we know we'll need some changing _parameter_ to affect\n",
        "the mathematical description. Sounds like we need equations! The equations that govern \n",
        "our system are often called _equations of motion_. Any system that changes due to \n",
        "some external \"force\" is known as a _dynamical_ system by definition. \n",
        "\n",
        "The first step to any controls problem is identifying the _dynamics_; this usually \n",
        "means defining the _equations of motion_ for our system. A set of _equations of motion_\n",
        "which describe our _dynamical_ system __is__ our model. \n",
        "\n",
        "## Example\n",
        "A really common system in engineering is known as the __spring-mass-damper__. \n",
        "We can roughly describe this system as a block on a table, which \n",
        "is connected to a spring. We can _force_ (a.k.a. _affect_) this system by pulling on \n",
        "the block to extend or compress the spring. \n",
        "\n",
        "### Model Construction\n",
        "Let's figure out our equations of motion. The following question is usually a useful \n",
        "starting point: \"what are the forces on our system?\" We know we'll have one external \n",
        "force: us pulling or pushing on the block! Let's call this external force $f_e$. \n",
        "We'll also have a force due to the spring, and a force due to the friction \n",
        "between the block and the table. The force due to the spring will be _proportional_\n",
        "to the position of the block with respect to the spring's neutral position: let's \n",
        "call the spring constant $k$. The force due to friction will be proportional \n",
        "to the _velocity_ of the block (let's forget about static friction for now):\n",
        "let's call the coefficient of friction $d$.\n",
        "\n",
        "With all of the forces identified, we can start constructing the _eqautions of motion_\n",
        "for this system. If we call our block's position $x$, then the acceleration $\\ddot{x}$\n",
        "will be equal to the sum of our external force $f_e$, the spring force $k x$, and the\n",
        "force due to friction $d \\dot{x}$. Summing these forces produces the following (common)\n",
        "spring-mass-damper equation, where $m$ is the mass of the block. \n",
        "\n",
        "$$f_e = m \\ddot{x} + d \\dot{x} + k x$$ {#eq-forced-mass-damper}\n",
        "\n",
        "We have our model! This is a second-order differential equation (@eq-forced-mass-damper). It helps to divide \n",
        "all variables into two groups: parameters, and states. States describe the system \n",
        "at some point in time. In this case, state variables answer the following questions:\n",
        "\"where is the block, what is the velocity of the block, and what is the acceleration\n",
        "of the block?\" One set of valid state variables for this system is $x$ and $\\dot{x}$; \n",
        "we don't need to include $\\ddot{x}$ in our list of state variables because we can \n",
        "calculate $\\ddot{x}$ from $x$ and $\\dot{x}$. \n",
        "\n",
        ":::{.callout-note}\n",
        "We say \"one set of state variables\" because there may (and nearly always are) other valid state representations that could completely describe this system! State variables are __not__ unique.\n",
        ":::\n",
        "\n",
        "If we leave states as symbolic, and then look to the equation(s)\n",
        "of motion of a system, the _parameters_ describe a specific _instance_ of a system.\n",
        "Put another way, no matter what values $f_e$, $d$, and $k$ take, this equation of motion\n",
        "is identifiable as a spring-mass-damper. For this reason, we can specify $f_e$, $d$, and $k$\n",
        "as parameters. We can assume a unit mass for the block for now.\n",
        "\n",
        ":::{.callout-note}\n",
        "The parameter $f_e$ is our _control_ parameter in this example, because we can change $f_e$\n",
        "to affect our system. For now, let's not differentiate between control parameters and \n",
        "non-control parameters. \n",
        ":::\n",
        "\n",
        "### Coding our Model\n",
        "\n",
        "We can simulate these dynamics with Julia's `DifferentialEquations` package. If you're \n",
        "familiar with MATLAB, `DifferentialEquations` provides numerical integration solvers \n",
        "that are similar to MATLAB's `ode45` (and similar) solvers. We can use the `ModelingToolkit`\n",
        "package to conveniently put our model to code, and interface with `DifferentialEquations`\n",
        "for simulation.\n"
      ],
      "id": "3d69a053"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "#| echo: false\n",
        "#| output: false\n",
        "using Logging\n",
        "Logging.disable_logging(Logging.Info)"
      ],
      "id": "14f5b695",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "#| output: false\n",
        "using ModelingToolkit, DifferentialEquations, Plots"
      ],
      "id": "75194eff",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "Now that we have access to `ModelingToolkit`, let's build the model!\n"
      ],
      "id": "f5065e65"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "@parameters t fₑ d k\n",
        "@variables x(t) ẋ(t)\n",
        "δ = Differential(t)\n",
        "\n",
        "eqs = [\n",
        "    δ(x) ~ ẋ,\n",
        "    δ(ẋ)~ - d*ẋ - k*x + fₑ\n",
        "]\n",
        "\n",
        "model = ODESystem(eqs, t, [x, ẋ], [fₑ, d, k]; name = :HarmonicOscillator)"
      ],
      "id": "234947d0",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "### Simulating our Model\n",
        "With `model` defined above, we can use `DifferentialEquations`\n",
        "to simulate our system. Of course, to do this we'll need to specify\n",
        "numerical values for our parameters, and initial conditions (the simulation\n",
        "starting point) for our state variables. The code below specifies some arbitrary\n",
        "initial conditions and constant parameter values, and simulates the resulting dynamics. \n"
      ],
      "id": "1848e8b6"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "problem = let x₀ = 0.1, ẋ₀ = 0.0, dₙ = 0.5, kₙ = 0.9, fₙ = 1.0, Δt = 30.0\n",
        "    ODEProblem(\n",
        "        model,\n",
        "        [x => x₀, ẋ => ẋ₀],\n",
        "        (0.0, Δt),\n",
        "        [d => dₙ, k => kₙ, fₑ => fₙ]\n",
        "    )\n",
        "end\n",
        "\n",
        "solutions = solve(problem, Tsit5(); reltol = 1e-12, abstol = 1e-12)\n",
        "plot(solutions; linewidth = 2, dpi = 130, title = \"Spring Mass Damper Simulation\")"
      ],
      "id": "a652c58e",
      "execution_count": null,
      "outputs": []
    }
  ],
  "metadata": {
    "kernelspec": {
      "name": "julia-1.7",
      "language": "julia",
      "display_name": "Julia 1.7.3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 5
}